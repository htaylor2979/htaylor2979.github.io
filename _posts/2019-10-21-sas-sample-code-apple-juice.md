---
author: Hellen Taylor
title: Predicting Bacterial Spores Growth with Logistic Regression in SAS
categories: sas
layout: post
permalink: sas-code-nisin-apple-juice
---
# Introduction

One of my class projects in Fall 2018 was to model the growth of bacterial spores called  <i>Alicyclobacillus acidoterrestris</i> CRA7152 in pasteurized apple juice. This particular bacterial strain is responsible for the "off" taste that develops in refrigerated fruit juices like those sold in cold cases at grocery stores. Though harmless, CRA7152 causes grocery stores to loose money by shortening products' shelf life, and its spores are resistant to the heat of pasteurization. 

Furtunately, a chemical called nisin reduces CRA7152's heat resistance so that more of the spores die when heated. Thus, my modeling goal was predict whether spores would grow in pasteurized apple juice based on the following features:
<ul>
	<li>nisin (units were not provided with the dataset, but typical units would be IU nisin/ml of solution)</li>
	<li>pH of the apple juice</li>
	<li>temperature of pasteurization in degrees Celcius</li>
	<li>Brix, which is a measure of percent-weight of sugar solutions</li>
</ul>

Because the response variable was binary (1 = growth of CRA7152 spores), I used logistic regression to predict the probability of a positive outcome. 

### SAS code used in exploring the data
I wrote the following SAS code to explore the model features. The project was graded, in part, on our thoroughness of characterizing the data before modeling. 

Table 1 summarizes the descriptive stats that were generated by the "Introduce the Data" section of code.

 ![Table 1 Summary Stats]({{ site.url }}/assets/img/nisin-apple-juice-dataset-stats-fewer-dec.jpg)

{% highlight ruby %}
	libname wk6 "/folders/myfolders/" ;

	/***** Introduce the Data *****/

	* Check the dataset file contents ;
	proc contents data=wk6.apple_juice ;
		label ;
	run ;

	proc freq data=wk6.apple_juice ;
	run ;

	* Generate descriptive stats ;
	ods graphics on;
	proc univariate data = wk6.apple_juice 
		outtable = applej_stats  
		noprint	;
	run;

	* Print a short table of descriptive stats ;
	title "Descriptive Stats for the Apple Juice Dataset" ;
	proc print data = applej_stats
		noobs label ;
		var _VAR_ _NOBS_ _MIN_ _MEDIAN_ _MEAN_ _MAX_ _SKEW_ _NMISS_ ;
		label 
			_MIN_="Minimum" 
			_MAX_="Maximum" 
			_MEAN_="Mean" 
			_MEDIAN_="Median" 
			_SKEW_ = "Skew"
			_NMISS_="# of Missing Values" ;
	run ;
	
{% endhighlight %}

{% highlight ruby %}
	/***** Investigate Each Predictor *****/

	/* Run Proc Logistic on the four predictors, one at a time. 
	* Implement a macro to save coding */

	%macro testgrowth(my_vars =, plots_wanted =,  ) ;
		title "Proc Logistic Output for Variable(s): &my_vars" ;
		ods graphics on ;
		proc logistic data=wk6.apple_juice
			plots = &plots_wanted. ;
			model growth (event='1') = &my_vars. ;
		run ;
	%mend ;

	/* Input: plots_wanted can be none, all, a list, or just one plot */
	%testgrowth(my_vars = ph, plots_wanted = ROC) ;
	%testgrowth(my_vars = nisin, plots_wanted = ROC) ;
	%testgrowth(my_vars = temperature, plots_wanted = ROC) ;
	%testgrowth(my_vars = brix, plots_wanted = ROC) ;
	* %testgrowth(my_vars = ph nisin temperature brix, plots_wanted = ROC) ;

	/* The model with all the variables gets its own code because 
	*  the selection method was specified in the homework instructions. */
	title "Proc Logistic Output for All Variables with Stepwise Selection Method" ;
	ods graphics on ;
	proc logistic data=wk6.apple_juice
		plots = ROC ;
		model growth (event='1') = ph nisin temperature brix
			/selection=stepwise;
	run ;
	
{% endhighlight %}
